// This file is part of www.nand2tetris.org
// and the book "The Elements of Computing Systems"
// by Nisan and Schocken, MIT Press.
// File name: projects/02/ALU.hdl

/**
 * The ALU (Arithmetic Logic Unit).
 * Computes one of the following functions:
 * x+y, x-y, y-x, 0, 1, -1, x, y, -x, -y, !x, !y,
 * x+1, y+1, x-1, y-1, x&y, x|y on two 16-bit inputs, 
 * according to 6 input bits denoted zx,nx,zy,ny,f,no.
 * In addition, the ALU computes two 1-bit outputs:
 * if the ALU output == 0, zr is set to 1; otherwise zr is set to 0;
 * if the ALU output < 0, ng is set to 1; otherwise ng is set to 0.
 */

// Implementation: the ALU logic manipulates the x and y inputs
// and operates on the resulting values, as follows:
// if (zx == 1) set x = 0        // 16-bit constant
// if (nx == 1) set x = !x       // bitwise not
// if (zy == 1) set y = 0        // 16-bit constant
// if (ny == 1) set y = !y       // bitwise not
// if (f == 1)  set out = x + y  // integer 2's complement addition
// if (f == 0)  set out = x & y  // bitwise and
// if (no == 1) set out = !out   // bitwise not
// if (out == 0) set zr = 1
// if (out < 0) set ng = 1

CHIP ALU {
    IN  
        x[16], y[16],  // 16-bit inputs        
        zx, // zero the x input?
        nx, // negate the x input?
        zy, // zero the y input?
        ny, // negate the y input?
        f,  // compute out = x + y (if 1) or x & y (if 0)
        no; // negate the out output?

    OUT 
        out[16], // 16-bit output
        zr, // 1 if (out == 0), 0 otherwise
        ng; // 1 if (out < 0),  0 otherwise

    PARTS:
    Not(in=zx, out=nzx);
    Extender16(in=nzx, out=nzx16);
    And16(a=x, b=nzx16, out=xorz);
    Not16(in=xorz, out=nxorz);
    Mux16(a=xorz, b=nxorz, sel=nx, out=xval);

    Not(in=zy, out=nzy);
    Extender16(in=nzy, out=nzy16);
    And16(a=y, b=nzy16, out=yorz);
    Not16(in=yorz, out=nyorz);
    Mux16(a=yorz, b=nyorz, sel=ny, out=yval);
    
    And16(a=xval, b=yval, out=xandy);
    Add16(a=xval, b=yval, out=xplusy);
    Mux16(a=xandy, b=xplusy, sel=f, out=function);
    Not16(in=function, out=nfunction);
    Mux16(a=function, b=nfunction, sel=no, out=fout);

    And16(a=true, b=fout, out[15]=ng);
    Or16Way(in=fout, out=nzr);
    Not(in=nzr, out=zr);

    Or16(a=fout, b=false, out=out);
}